/*
 * Copyright 2016 Philip Cohn-Cort
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'

// Note that the versions here are determined as follows:
/**
 * targetVersion equals the most recent stable, released SDK version. The sources
 * for that version must be open to the public.
 */
def targetVersion = 24
/**
 * supportVersion is the oldest version of the support libraries against which this
 * codebase can compile.
 */
def supportVersion = '22.2.0'

// Maven deployment info (the name of the artifact is derived from the containing folder's name)
group = 'com.fuzz'
version = '0.1'

// Concept and execution for this configuration come from http://stackoverflow.com/q/29663918
configurations {
    javadocDeps
}

android {
    compileSdkVersion targetVersion
    buildToolsVersion '24.0.2'

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion targetVersion
        versionCode 1
        versionName "$version"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main.res.srcDirs = ["src/main/res"]
        main.java.srcDirs = ["src/main/java"]
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    // Required for ViewPager-aware listeners and so forth
    compile "com.android.support:appcompat-v7:$supportVersion"
    testCompile 'junit:junit:4.12'

    //noinspection GradleDependency - the lint warning a few lines up will suffice.
    javadocDeps "com.android.support:appcompat-v7:$supportVersion"
    javadocDeps "com.android.support:support-v4:$supportVersion"
}



// Everything below this point is here to make Maven artifact creation easier.

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    classpath += configurations.javadocDeps
    destinationDir = file("../javadoc/")
    failOnError false
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

// A proper sources jar, with comments
task androidSourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

// Request androidSources and javadoc artifacts
artifacts {
    archives androidSourcesJar
    archives javadocJar
}